#include "dodge.h"
#include <Arduino.h>

void drawPlayer();
void erasePlayer();
void drawEnemy(int index);
void eraseEnemy(int index);
void updateEnemies();
void spawnEnemy();
boolean checkCollision();
void drawDodgeGameArea();
void updateDodgeScore();
void showDodgeGameOver();
void resetDodgeGame();

// Game constants
const short GAME_WIDTH = 128;
const short GAME_HEIGHT = 128;
const int cat_width = 25;
const int cat_height = 16;
const short ENEMY_SIZE = 16;
const short MAX_ENEMIES = 5;
const short PLAYER_SPEED = 3;

// 25x16 cat sprite
const uint16_t CAT[400] PROGMEM = {
  0x0000,0x0000,0x3900,0x30E0,0x0000,0x0000,0x0000,0x0000,0x0000,0x3900,0x30E0,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x0000,0x4120,0xD3A0,0xC2C0,0x30E0,0x0000,0x0000,0x0000,0x3920,0xCAE0,0xCB80,0x30E0,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x0000,0x5160,0xF3A0,0xF420,0xCB80,0x3900,0x3900,0x4120,0xD3A0,0xFC20,0xEB80,0x4140,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x0000,0x5160,0xFC20,0xFC60,0xFC40,0xD3A0,0xCAE0,0xCB00,0xFC60,0xFC60,0xF400,0x4140,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x3920,0xCB00,0xFC60,0xFC60,0xFC40,0xF380,0xEB60,0xFC20,0xFC60,0xFC60,0xFC40,0xC2C0,0x30E0,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x5160,0xFC20,0xFC60,0xFC60,0xFC60,0xFC20,0xFC20,0xFC60,0xFC60,0xFC60,0xFC60,0xF400,0x4140,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x5160,0xFC60,0xF440,0x69E0,0x7200,0xFC60,0xF440,0x69E0,0x7200,0xFC60,0xFC60,0xF420,0x4140,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x5160,0xFC60,0xFC40,0xD3A0,0xD3C0,0xFC60,0xFC40,0xD3A0,0xD3C0,0xFC60,0xFC60,0xF420,0x4140,0x3900,0x30E0,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x5160,0xF3A0,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC40,0xEB80,0x4960,0xD3A0,0xCB80,0x3900,0x3900,0x30E0,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,
  0x1060,0x71E0,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xF420,0xD3C0,0xFC60,0xF420,0x4960,0xD3A0,0xCB80,0x3900,0x3900,0x30E0,0x0000,0x0000,0x0000,0x0000,
  0x0000,0x5160,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xF420,0x4940,0x69E0,0x7200,0xD3A0,0xD3A0,0xCB80,0x30E0,0x0000,0x0000,0x0000,
  0x0000,0x5160,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xFC60,0xF420,0x4140,0x1040,0x1060,0x69E0,0x69E0,0x7200,0xCB80,0x30E0,0x0000,0x0000,
  0x0000,0x1060,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x69E0,0x0840,0x0000,0x0000,0x1040,0x1060,0x5160,0xF420,0x4140,0x0000,0x0000,
  0x0000,0x0000,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x1040,0x0840,0x0000,0x0000,0x0000,0x0000,0x3900,0x5160,0xF420,0x4140,0x0000,0x0000,
  0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x4120,0xD3A0,0xCBA0,0x69E0,0x0840,0x0000,0x0000,
  0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x0000,0x1060,0x69E0,0x69E0,0x0840,0x0000,0x0000,0x0000
};


// 16x16 enemy sprite MARIO
const uint16_t PROGMEM enemySprite[256] = {
  0x0000,0x0000,0x0000,0xF800,0xD120,0xD920,0xD920,0xD920,0xD920,0xD920,0xD920,0xD140,0xD140,0xBA00,0x0000,0x0000,
  0x0000,0x0000,0x07E0,0xB9E0,0xC960,0xD160,0xD160,0xD980,0xD980,0xD160,0xD980,0xD940,0xD120,0xD160,0x0000,0x0000,
  0x0000,0x8400,0xA2A0,0xA2A0,0xB2A0,0xAA80,0xBAE1,0xF425,0xEC05,0xBAE1,0xEC05,0xF365,0xE2E3,0xD2A5,0x0000,0x0000,
  0x0000,0xA2A0,0xA2A0,0xAAE0,0xDC04,0xB301,0xE404,0xFCC6,0xF486,0xC342,0xEC45,0xFCC7,0xFCC7,0xFCC7,0xFC10,0x0000,
  0x0000,0xA280,0xA2A0,0xAAE0,0xE404,0xBB21,0xC342,0xF486,0xFCA6,0xE424,0xBB42,0xEC45,0xF486,0xFCA7,0xFCC8,0x0000,
  0x0000,0xA2A0,0xA2A0,0xA2A0,0xBB42,0xEC45,0xF486,0xFCC7,0xF486,0xC342,0xB2E1,0xB321,0xB301,0xD3C4,0xFFF0,0x0000,
  0x0000,0xAAA0,0x9A80,0xBB42,0xE425,0xF466,0xFC66,0xF466,0xF466,0xEC45,0xF466,0xF466,0xCBA3,0x0000,0x0000,0x0000,
  0x0000,0x0000,0x9AA0,0xA2A0,0xAAE0,0xB2C0,0xD1E1,0xB2C0,0xAAE0,0xB2C1,0xC363,0xCBA4,0xB301,0x8400,0x0000,0x0000,
  0x8400,0x9A80,0xA2A0,0xA2A0,0xA2A0,0xAA60,0xC960,0xAA40,0xAA40,0xC980,0xAA60,0xA2A0,0xA2A0,0x9A80,0x8400,0x0000,
  0xAAE1,0xAAE1,0xAAE0,0xAAE0,0xA2A0,0xB260,0xD1A0,0xD160,0xD160,0xD1A0,0xB260,0xA2A0,0xAAE0,0xAAE1,0xAAE1,0x0000,
  0xF486,0xF466,0xF486,0xEC45,0xBAE1,0xD1E0,0xEB44,0xD980,0xD980,0xEB44,0xD1E0,0xBAE1,0xEC45,0xF486,0xECA6,0x0000,
  0xFCC7,0xFCC7,0xFCC7,0xFCA6,0xEBC5,0xD980,0xD980,0xD940,0xD940,0xD980,0xD980,0xEBC4,0xFCA6,0xFCC7,0xFCE7,0x0000,
  0xF4E7,0xFCC7,0xFCA7,0xF405,0xE1E1,0xD920,0xD920,0xD920,0xD920,0xD920,0xD920,0xE1E1,0xFC46,0xFCC7,0xF4A7,0x0000,
  0xFDE8,0xF4C6,0xD303,0xD1C1,0xD160,0xD140,0xD120,0xD140,0xD120,0xD140,0xD140,0xD160,0xD2C2,0xF4C8,0xFD4A,0x0000,
  0x8400,0x9A80,0xA2A0,0xAA60,0xAA60,0xAA60,0xB9E0,0xF800,0xB9E0,0xAA60,0xAA60,0xAA80,0xA2A0,0x9A80,0x8400,0x0000,
  0xA2A0,0xA2A0,0xA2A0,0xA2A0,0xA2A0,0xA2A0,0x9AC0,0x0000,0x9AC0,0xA2A0,0xA2A0,0xA2A0,0xA2A0,0xA2A0,0x9AC0,0x0000
};

struct Enemy {
  int x;
  int y;
  int oldY;
  int speed;
  boolean active;
};

int playerX;
int playerY;
int oldPlayerX;
int oldPlayerY;
Enemy enemies[MAX_ENEMIES];
int dodgeScore;
int dodgeOldScore;
unsigned long lastSpawn;
unsigned long spawnInterval;
unsigned long gameTimer;
unsigned long lastUpdate;
boolean dodgeGameOver;
boolean dodgeReturnToMenu;

const int JOY_THRESHOLD = 200;
const int JOY_CENTER = 512;

void drawPlayer() {
  tft.drawRGBBitmap(playerX, playerY, (const uint16_t*)CAT, cat_width, cat_height);
}

void erasePlayer() {
  tft.fillRect(oldPlayerX, oldPlayerY, cat_width, cat_height, ST7735_BLACK);
}

void drawEnemy(int index) {
  if (enemies[index].active) {
    tft.drawRGBBitmap(enemies[index].x, enemies[index].y, (const uint16_t *)enemySprite, ENEMY_SIZE, ENEMY_SIZE);
  }
}

void eraseEnemyAtPosition(int index, int yPos) {
  tft.fillRect(enemies[index].x, yPos, ENEMY_SIZE, ENEMY_SIZE, ST7735_BLACK);
}

void updateEnemies() {
  for (int i = 0; i < MAX_ENEMIES; i++) {
    if (enemies[i].active) {
      enemies[i].oldY = enemies[i].y;
      enemies[i].y += enemies[i].speed;
      
      if (enemies[i].y > GAME_HEIGHT) {
        tft.fillRect(enemies[i].x, enemies[i].oldY, ENEMY_SIZE, ENEMY_SIZE, ST7735_BLACK);
        enemies[i].active = false;
        dodgeScore += 10;
      }
    }
  }
}

void renderEnemies() {
  boolean scoreNeedsRedraw = false;

  for (int i = 0; i < MAX_ENEMIES; i++) {
    if (enemies[i].active) {
      drawEnemy(i);

      // Erase the gap between old and new position
      int gap = enemies[i].y - enemies[i].oldY;
      if (gap > 0) {
        tft.fillRect(enemies[i].x, enemies[i].oldY, ENEMY_SIZE, gap, ST7735_BLACK);
      }

      // Check if enemy overlaps the score display
      if (enemies[i].y < 15) {
        scoreNeedsRedraw = true;
      }
    }
  }

  // Redraw score if enemy overlapped it
  if (scoreNeedsRedraw) {
    dodgeOldScore = -1;
    updateDodgeScore();
    
    tft.setTextColor(ST7735_WHITE);
    tft.setCursor(5, 5);
    tft.print("SCORE:");
  }
}

void spawnEnemy() {
  for (int i = 0; i < MAX_ENEMIES; i++) {
    if (!enemies[i].active) {
      enemies[i].x = random(0, GAME_WIDTH - ENEMY_SIZE);
      enemies[i].y = -ENEMY_SIZE;
      enemies[i].oldY = enemies[i].y;
      enemies[i].speed = random(2, 4);
      enemies[i].active = true;
      break;
    }
  }
  
  // Gradually increase difficulty by reducing spawn interval
  if (spawnInterval > 800) {
    spawnInterval -= 20;
  }
}

boolean checkCollision() {
  for (int i = 0; i < MAX_ENEMIES; i++) {
    if (enemies[i].active) {
      if (playerX < enemies[i].x + ENEMY_SIZE - 4 &&
          playerX + cat_width - 4 > enemies[i].x &&
          playerY < enemies[i].y + ENEMY_SIZE - 4 &&
          playerY + cat_height - 4 > enemies[i].y) {
        return true;
      }
    }
  }
  return false;
}

void drawDodgeGameArea() {
  tft.fillScreen(ST7735_BLACK);
  
  tft.setTextColor(ST7735_WHITE);
  tft.setTextSize(1);
  tft.setCursor(5, 5);
  tft.print("SCORE:");
  
  updateDodgeScore();
}

void updateDodgeScore() {
  if (dodgeScore != dodgeOldScore) {
    tft.fillRect(45, 5, 30, 8, ST7735_BLACK);
    
    tft.setTextColor(ST7735_WHITE);
    tft.setTextSize(1);
    tft.setCursor(45, 5);
    tft.print(dodgeScore);
    
    dodgeOldScore = dodgeScore;
  }
}

void showDodgeGameOver() {
  for (int i = 0; i < 3; i++) {
    tft.invertDisplay(true);
    delay(100);
    tft.invertDisplay(false);
    delay(100);
  }
  
  tft.fillRect(15, 40, 98, 70, ST7735_BLACK);
  tft.drawRect(15, 40, 98, 70, ST7735_RED);
  tft.drawRect(16, 41, 96, 68, ST7735_RED);
  
  tft.setTextColor(ST7735_RED);
  tft.setTextSize(2);
  tft.setCursor(25, 48);
  tft.print("GAME");
  tft.setCursor(25, 64);
  tft.print("OVER");
  
  tft.setTextColor(ST7735_WHITE);
  tft.setTextSize(1);
  tft.setCursor(30, 85);
  tft.print("Score: ");
  tft.print(dodgeScore);
  
  tft.setTextColor(ST7735_YELLOW);
  tft.setCursor(20, 100);
  tft.print("Press to menu");
  
  dodgeGameOver = true;
}

void resetDodgeGame() {
  playerX = GAME_WIDTH / 2 - cat_width / 2;
  playerY = GAME_HEIGHT - cat_height - 10;
  oldPlayerX = playerX;
  oldPlayerY = playerY;
  
  for (int i = 0; i < MAX_ENEMIES; i++) {
    enemies[i].active = false;
  }
  
  dodgeScore = 0;
  dodgeOldScore = 0;
  lastSpawn = millis();
  lastUpdate = millis();
  spawnInterval = 2000;  // Start slower
  gameTimer = millis();
  dodgeGameOver = false;
  dodgeReturnToMenu = false;
  
  drawDodgeGameArea();
  drawPlayer();
}

void dodgeSetup() {
  tft.fillScreen(ST7735_BLACK);
  
  tft.setTextColor(ST7735_CYAN);
  tft.setTextSize(2);
  tft.setCursor(30, 40);
  tft.print("DODGE");
  tft.setCursor(15, 60);
  tft.print("MARIO!");
  delay(1500);
  
  resetDodgeGame();
}

void dodgeLoop() {
  if (dodgeGameOver) {
      if (!digitalRead(joyButton) || analogRead(joyY) < JOY_CENTER - JOY_THRESHOLD || 
          analogRead(joyY) > JOY_CENTER + JOY_THRESHOLD || analogRead(joyX) < JOY_CENTER - JOY_THRESHOLD || 
          analogRead(joyX) > JOY_CENTER + JOY_THRESHOLD || !digitalRead(button)) {
        delay(500);
        dodgeReturnToMenu = true;
      }
      return;
    }
    
    unsigned long currentTime = millis();
    
    // Update at 30-40 FPS (approx 25-30ms) for better stability 
    if (currentTime - lastUpdate >= 25) { 
      lastUpdate = currentTime;

      int joyXVal = analogRead(joyX);
      int nextX = playerX;
      
      if (joyXVal > JOY_CENTER + JOY_THRESHOLD) {
        nextX -= PLAYER_SPEED;
        if (nextX < 0) nextX = 0;
      } else if (joyXVal < JOY_CENTER - JOY_THRESHOLD) {
        nextX += PLAYER_SPEED;
        if (nextX > GAME_WIDTH - cat_width) nextX = GAME_WIDTH - cat_width;
      }

      // Update player only if position changed
      if (nextX != playerX) {
        int oldX = playerX;
        playerX = nextX;
        drawPlayer();
        // Erase the area player moved away from
        if (nextX > oldX) {
          tft.fillRect(oldX, playerY, nextX - oldX, cat_height, ST7735_BLACK);
        } else {
          tft.fillRect(nextX + cat_width, playerY, oldX - nextX, cat_height, ST7735_BLACK);
        }
      } else {
        drawPlayer();
      }
      
      updateEnemies();
      renderEnemies();
      
      if (checkCollision()) {
        showDodgeGameOver();
        return;
      }
      updateDodgeScore();
    }

    if (currentTime - lastSpawn > spawnInterval) {
      spawnEnemy();
      lastSpawn = currentTime;
    }
}

bool dodgeCheckReturnToMenu() {
  return dodgeReturnToMenu;
}